buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.2.3'
  }
}

plugins {
  id "com.jfrog.bintray"              version "1.7.3"
  id "com.github.hierynomus.license"  version "0.14.0"
}

ext {
  //semver-git config
  nextVersion = "patch"
  snapshotSuffix = "SNAPSHOT"
}
apply plugin: 'com.cinnober.gradle.semver-git'

group 'cx.cad.keepachangelog'

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
  compile     group: 'com.vladsch.flexmark',  name: 'flexmark',     version: '0.19.6'
  compile     group: 'com.github.zafarkhaja', name: 'java-semver',  version: '0.9.0'
  testCompile group: 'junit',                 name: 'junit',        version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

def pomConfig = {
  /* XXX Fix this when the plugins are fixed or a workaround is found.

  The license plugin creates a conflict with the way that the Bintray plugin
  recommends configuring the POM. It enables the creation of poorly formed XML
  because the closure doesn't resolve correctly. Changing "license" below to be
  a String does not work, either.

  https://stackoverflow.com/questions/43820811/using-the-bintray-plugin-and-license-gradle-plugin-together
  https://github.com/bintray/gradle-bintray-plugin/issues/183

  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  */
  developers {
    developer {
      id "colindean"
      name "Colin Dean"
      email "git@cad.cx"
    }
  }

  scm {
    url "https://github.com/colindean/keepachangelog-parser-java"
  }
}

publishing {
  publications {
    Bintray(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'cx.cad.keepachangelog'
      artifactId 'changelog-parser'
      version project.version
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'A parser for keepachangelog Markdown-formatted changelogs')
        root.appendNode('name', 'keepachangelog-parser-java')
        root.appendNode('url', 'https://github.com/colindean/keepachangelog-parser-java')
        root.children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Bintray']
  pkg {
    repo = 'cx.cad.keepachangelog'
    name = 'changelog-parser'
    desc = 'A parser for keepachangelog Markdown-formatted changelogs'
    licenses = ['Apache-2.0']
    websiteUrl = 'https://github.com/colindean/keepachangelog-parser-java'
    issueTrackerUrl = 'https://github.com/colindean/keepachangelog-parser-java/issues'
    vcsUrl = 'https://github.com/colindean/keepachangelog-parser-java.git'
    githubRepo = 'colindean/keepachangelog-parser-java'
    githubReleaseNotesFile = 'README.md'
    version {
      name = project.version
      desc = "keepachangelog parser for Java ${project.version}"
      released  = new Date()
      vcsTag = project.version
    }
  }
}

jar {
  manifest {
    attributes 'Implementation-Title': 'keepachangelog-parser-java',
               'Implementation-Version': version,
               'Implementation-Vendor': 'Colin Dean'
  }
  from "LICENSE.md"
}

license {
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.copyright = 'Colin Dean'
  header = file('.copyright_header')
  strictCheck true
}
